[doc Sanity checks for the trafo address and structure examples]

[include trafo-ex-common.luxinc]

[shell genet_cli]
    [invoke genet_cli_initial]

    !no example-domain address
    !commit
    ?Commit complete|No modifications to commit

    # address tests

    [invoke test-ex-common "address constant a" 23 $noentries]

    [invoke test-ex-common "address dynamic choice a" abcd $noentries]
    [invoke verify-ex-device-noexists "address dynamic choice k"]
    [invoke test-ex-common "address dynamic choice d" true false]
    [invoke verify-ex-device-noexists "address dynamic choice l"]
    [invoke verify-ex-device "address dynamic choice k" abcd]
    !no example-domain address dynamic choice d
    [invoke commit]
    [invoke verify-ex-device-noexists "address dynamic choice k"]
    [invoke verify-ex-device-noexists "address dynamic choice l"]
    [invoke test-ex-common "address dynamic choice a" abcd $noentries]
    !no example-domain address dynamic choice a
    [invoke commit]
    [invoke verify-ex-device-noexists "address dynamic choice k"]
    [invoke verify-ex-device-noexists "address dynamic choice l"]

    [invoke test-ex-common "address dynamic lists l" 2-123 $noentries]
    [invoke test-ex-common "address dynamic lists l" 1-123 2-123]
    !example-domain address dynamic lists l 1-321
    !example-domain address dynamic lists l 3-11
    [invoke commit]
    !do show running-config example-domain address dynamic lists
    """?
    example-domain address dynamic lists l 1-123
    !
    example-domain address dynamic lists l 1-321
    !
    example-domain address dynamic lists l 2-123
    !
    example-domain address dynamic lists l 3-11
    !
    genet-cfg#
    """
    !do show running-config example-device address dynamic lists
    """?
    example-device address dynamic lists l1 123
    !
    example-device address dynamic lists l1 321
    !
    example-device address dynamic lists l2 123
    !
    example-device address dynamic lists l3 11
    !
    genet-cfg#
    """

    [invoke test-ex-common "address key-constant a" "abcd 179" $noentries]
    [invoke test-ex-common "address key-constant a" "efgh 179" "abcd 179"]
    # failure regexp would match - unset it for a moment
    -
    !example-domain address key-constant a ijkl 12
    ?Error:.*"Only the value 179 is supported"
    -[Ee][Rr][Rr][Oo][Rr]

    [invoke test-ex-common "address key-offset a" 0 $noentries]
    [invoke test-ex-common "address key-offset a" 7 0]
    !do show running-config example-device address key-offset
    """?
    example-device address key-offset l 1
    !
    example-device address key-offset l 8
    !
    genet-cfg#
    """

    # TODO: no lookup table tests

    # structure domain tests
    !no example-domain structure
    !commit
    ?Commit complete|No modifications to commit

    [invoke test-ex-common "structure leafs one-to-multiple-copies a" abcd $noentries]
    [invoke verify-ex-device "structure leafs one-to-multiple-copies k" abcd]
    [invoke verify-ex-device "structure leafs one-to-multiple-copies l" abcd]

    [invoke test-ex-common "structure leafs one-to-multiple-parts a" 10.1.2.0/24 $noentries]
    [invoke verify-ex-device "structure leafs one-to-multiple-parts k" 10.1.2.0]
    [invoke verify-ex-device "structure leafs one-to-multiple-parts l" 255.255.255.0]

    !do show running-config example-domain structure leafs many-to-one a
    ?$noentries
    !example-domain structure leafs many-to-one a 123
    [invoke test-ex-common "structure leafs many-to-one b" 456 $noentries]
    [invoke test-ex-common "structure leafs many-to-one a" 789 123]
    [invoke verify-ex-device "structure leafs many-to-one k" 789:456]

    [invoke test-ex-common "structure lists split-by-cols a" 12 $noentries]
    """!
    example-domain structure lists split-by-cols a 12 d abcd
    example-domain structure lists split-by-cols a 12 e 10.1.2.3
    example-device structure lists split-by-cols k 13 n efgh
    example-device structure lists split-by-cols l 14 o 10.2.3.4
    """
    [invoke commit]
    !do show running-config example-domain structure lists split-by-cols
    """?
    do show running.*
    example-domain structure lists split-by-cols a 12
     d abcd
     e 10.1.2.3
    !
    example-domain structure lists split-by-cols a 13
     d efgh
    !
    example-domain structure lists split-by-cols a 14
     e 10.2.3.4
    !
    genet-cfg#
    """

    """!
    example-domain structure lists split-by-rows union a apache c ipv4
    example-domain structure lists split-by-rows union a docker c both d "docker both"
    """
    [invoke commit]
    !do show running-config example-domain structure lists split-by-rows union
    """?
    example-domain structure lists split-by-rows union a apache
     c ipv4
    !
    example-domain structure lists split-by-rows union a docker
     c both
     d "docker both"
    !
    genet-cfg#
    """
    !do show running-config example-device structure lists split-by-rows union
    """?
    example-device structure lists split-by-rows union ipv4 apache
    !
    example-device structure lists split-by-rows union ipv4 docker
     n "docker both"
    !
    example-device structure lists split-by-rows union ipv6 docker
     n "docker both"
    !
    genet-cfg#
    """
    """!
    no example-domain structure lists split-by-rows union a docker
    example-domain structure lists split-by-rows union a apache c both
    """
    [invoke commit]
    !do show running-config example-device structure lists split-by-rows union
    """?
    example-device structure lists split-by-rows union ipv4 apache
    !
    example-device structure lists split-by-rows union ipv6 apache
    !
    genet-cfg#
    """

    """!
    example-domain structure lists split-by-rows join a apache ipv4 d ready
    example-domain structure lists split-by-rows join a apache ipv6 d on-hold
    example-domain structure lists split-by-rows join a ssh ipv6 d available
    example-domain structure lists split-by-rows join a docker ipv4
    """
    [invoke commit]
    !do show running-config example-domain structure lists split-by-rows join
    """?
    example-domain structure lists split-by-rows join a ssh ipv6
     d available
    !
    example-domain structure lists split-by-rows join a apache ipv4
     d ready
    !
    example-domain structure lists split-by-rows join a apache ipv6
     d on-hold
    !
    example-domain structure lists split-by-rows join a docker ipv4
    !
    """
    !no example-domain structure lists split-by-rows join a docker ipv4
    [invoke commit]
    !do show running-config example-device structure lists split-by-rows join
    """?
    example-device structure lists split-by-rows join ipv4 apache
     n ready
    !
    example-device structure lists split-by-rows join ipv6 ssh
     n available
    !
    example-device structure lists split-by-rows join ipv6 apache
     n on-hold
    !
    genet-cfg#
    """

    """!
    example-domain structure lists sequencing a 12 c first
    example-domain structure lists sequencing a 111 c last
    example-domain structure lists sequencing a 33
    """
    [invoke commit]
    !do show running-config example-domain structure lists sequencing
    """?
    example-domain structure lists sequencing a 12
     c first
    !
    example-domain structure lists sequencing a 33
    !
    example-domain structure lists sequencing a 111
     c last
    !
    genet-cfg#
    """
    !do show running-config example-device structure lists sequencing
    """?
    example-device structure lists sequencing k 111
     m last
    !
    example-device structure lists sequencing k 12
     m first
    !
    example-device structure lists sequencing k 33
    !
    genet-cfg#
    """

    """!
    example-domain structure nested-lists nested-list-to-flat a 12 c abcd e false
    example-domain structure nested-lists nested-list-to-flat a 12 c efgh e true
    example-domain structure nested-lists nested-list-to-flat a 34 c ijkl
    """
    [invoke commit]
    !do show running-config example-domain structure nested-lists nested-list-to-flat
    """?
    example-domain structure nested-lists nested-list-to-flat a 12
     c abcd
      e false
     !
     c efgh
      e true
     !
    !
    example-domain structure nested-lists nested-list-to-flat a 34
     c ijkl
     !
    !
    genet-cfg#
    """

    [invoke test-ex-common "structure nested-lists flat-list-to-nested a" "10.1.2.3 abcd" $noentries]
    !do show running-config example-domain structure nested-lists flat-list-to-nested a 10.1.2.3 abcd d
    ?$noentries
    !example-domain structure nested-lists flat-list-to-nested a 10.1.2.3 abcd d 12
    [invoke commit]
    !do show running-config example-domain structure nested-lists flat-list-to-nested a 10.1.2.3 abcd d
    ?d 12
    """!
    example-domain structure nested-lists flat-list-to-nested a 10.1.2.3 efgh
    example-domain structure nested-lists flat-list-to-nested a 10.1.2.4 ijkl
    """
    [invoke commit]
    !do show running-config example-domain structure nested-lists flat-list-to-nested
    """?
    example-domain structure nested-lists flat-list-to-nested a 10.1.2.3 abcd
     d 12
    !
    example-domain structure nested-lists flat-list-to-nested a 10.1.2.3 efgh
    !
    example-domain structure nested-lists flat-list-to-nested a 10.1.2.4 ijkl
    !
    genet-cfg#
    """
    !do show running-config example-device structure nested-lists flat-list-to-nested
    """?
    example-device structure nested-lists flat-list-to-nested k 10.1.2.3
     m abcd
      o 12
     !
     m efgh
     !
    !
    example-device structure nested-lists flat-list-to-nested k 10.1.2.4
     m ijkl
     !
    !
    genet-cfg#
    """
    !no example-domain structure nested-lists flat-list-to-nested a 10.1.2.4 ijkl
    [invoke commit]
    !do show running-config example-domain structure nested-lists flat-list-to-nested
    """?
    example-domain structure nested-lists flat-list-to-nested a 10.1.2.3 abcd
     d 12
    !
    example-domain structure nested-lists flat-list-to-nested a 10.1.2.3 efgh
    !
    genet-cfg#
    """
    !do show running-config example-device structure nested-lists flat-list-to-nested
    """?
    example-device structure nested-lists flat-list-to-nested k 10.1.2.3
     m abcd
      o 12
     !
     m efgh
     !
    !
    genet-cfg#
    """

    # structure/nesting domain tests

    !no example-domain nesting
    !commit
    ?Commit complete|No modifications to commit

    [invoke test-ex-common "nesting address-value a" 0abcd $noentries]
    [invoke verify-ex-device "nesting address-value l" abcd]
    [invoke test-ex-common "nesting address-value a" 1efgh 0abcd]
    [invoke verify-ex-device "nesting address-value m" efgh]

    [invoke test-ex-common "nesting existence-address" c $noentries]
    [invoke verify-ex-device "nesting existence-address k" b0]
    !example-domain nesting existence-address d
    [invoke commit]
    !do show running-config example-domain nesting existence-address
    """?
    example-domain nesting existence-address c
    example-domain nesting existence-address d
    genet-cfg#
    """
    !no example-domain nesting existence-address d
    [invoke commit]
    [invoke verify-ex-device "nesting existence-address k" b0]
    !example-domain nesting existence-address d
    [invoke commit]

    [invoke test-ex-common "nesting existence-address" f c]
    [invoke verify-ex-device "nesting existence-address k" e0]
    !example-domain nesting existence-address g
    [invoke commit]
    !do show running-config example-domain nesting existence-address
    """?
    example-domain nesting existence-address f
    example-domain nesting existence-address g
    genet-cfg#
    """

    # TODO: implement

[cleanup]
    [invoke genet_cli]
    !config
    !no example-domain address
    !no example-domain structure
    !no example-domain nesting
    !commit
    ?Commit complete|No modifications to commit
    [invoke verify-ex-device-noexists address]
    [invoke verify-ex-device-noexists structure]
    [invoke verify-ex-device-noexists nesting]
